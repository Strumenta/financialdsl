buildscript {
    ext.kotlin_version = '1.2.60'
    ext.antlrKotlinVersion = "6167dc7090"

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.strumenta.antlr-kotlin:antlr-kotlin-gradle-plugin:${antlrKotlinVersion}"
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'com.strumenta.antlrkotlin'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "com.strumenta.antlr-kotlin:antlr-kotlin-runtime-common:${antlrKotlinVersion}"
    compile "com.strumenta.antlr-kotlin:antlr-kotlin-runtime-jvm:${antlrKotlinVersion}"

    compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

def genFolder = new File( projectDir.path + '/generated-src/antlr/' )

task('ensureGeneratedSrcAntlrExist') {
    doLast {        
        if( !genFolder.exists() ) {
            println("Creating directory $genFolder")
            genFolder.mkdirs()
        }
    }
}

generateKotlinGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'com.strumenta.financialdsl']
    outputDirectory = new File( genFolder.toString())
}
generateKotlinGrammarSource.dependsOn ensureGeneratedSrcAntlrExist
compileKotlin.dependsOn generateKotlinGrammarSource
idea.dependsOn generateKotlinGrammarSource
sourceSets {
    generated {
        kotlin.srcDir genFolder
    }
}
compileKotlin.source sourceSets.generated.kotlin, sourceSets.main.kotlin

clean{
    delete genFolder
}

idea {
    module {
        sourceDirs += genFolder
    }
}

test {
    dependsOn 'cleanTest'
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task listAllDependencies(type: DependencyReportTask) {}


